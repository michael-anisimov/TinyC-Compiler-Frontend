\section{Program Structure}
\begin{grammar}
<PROGRAM> ::= <PROGRAM_ITEM> <PROGRAM> \ | \ \epsilon \\

<PROGRAM_ITEM> ::= <NON_VOID_TYPE> identifier <NOT_VOID_FUNC_OR_VAR> \\
    \ | \ void <VOID_DECL_TAIL> \\
    \ | \ <STRUCT_DECL> \\
    \ | \ <FUNPTR_DECL> \\

<NOT_VOID_FUNC_OR_VAR> ::= <VARIABLE_TAIL> \ | \ <FUNC_DECL_TAIL> \\

<VOID_DECL_TAIL> ::= identifier <FUNC_DECL_TAIL> \\
    \ | \ <STAR_PLUS> identifier <FUNC_OR_VAR_TAIL> \\

<FUNC_OR_VAR_TAIL> ::= <VARIABLE_TAIL> \ | \ <FUNC_DECL_TAIL> \\

<VARIABLE_TAIL> ::= <OPT_ARRAY_SIZE> <OPT_INIT> <MORE_GLOBAL_VARS> ";" \\

<FUNC_DECL_TAIL> ::= "(" <OPT_FUN_ARGS> ")" <FUNC_TAIL> \\

<FUNC_TAIL> ::= <BLOCK_STMT> \ | \ ";" \\

<MORE_GLOBAL_VARS> ::= "," identifier <OPT_ARRAY_SIZE> <OPT_INIT> <MORE_GLOBAL_VARS> \ | \ \epsilon \\

<OPT_FUN_ARGS> ::= <FUN_ARG> <FUN_ARG_TAIL> \ | \ \epsilon \\

<FUN_ARG_TAIL> ::= "," <FUN_ARG> <FUN_ARG_TAIL> \ | \ \epsilon \\

<FUN_ARG> ::= <TYPE> identifier
\end{grammar}

\section{Statements}
\begin{grammar}
<STATEMENT> ::= <BLOCK_STMT> \ | \ <IF_STMT> \ | \ <SWITCH_STMT> \\
    \ | \ <WHILE_STMT> \ | \ <DO_WHILE_STMT> \ | \ <FOR_STMT> \\
    \ | \ <BREAK_STMT> \ | \ <CONTINUE_STMT> \ | \ <RETURN_STMT> \\
    \ | \ <EXPR_STMT> \\

<BLOCK_STMT> ::= "{" <STATEMENT_STAR> "}" \\

<STATEMENT_STAR> ::= <STATEMENT> <STATEMENT_STAR> \ | \ \epsilon \\

<IF_STMT> ::= "if" "(" <EXPR> ")" <STATEMENT> <ELSE_PART> \\

<ELSE_PART> ::= "else" <STATEMENT> \ | \ \epsilon \\

<SWITCH_STMT> ::= "switch" "(" <EXPR> ")" "{" <CASE_DEF_STMT_STAR> "}" \\

<CASE_DEF_STMT_STAR> ::= <CASE_STMT> <CASE_DEF_STMT_STAR> \\
    \ | \ \epsilon \ | \ <DEFAULT_CASE> <CASE_STMT_STAR> \\

<CASE_STMT> ::= "case" integer_literal ":" <CASE_BODY> \\

<CASE_BODY> ::= <STATEMENT_STAR> \\

<DEFAULT_CASE> ::= "default" ":" <CASE_BODY> \\

<WHILE_STMT> ::= "while" "(" <EXPR> ")" <STATEMENT> \\

<DO_WHILE_STMT> ::= "do" <STATEMENT> "while" "(" <EXPR> ")" ";" \\

<FOR_STMT> ::= "for" "(" <OPT_EXPR_OR_VAR_DECL> ";" <OPT_EXPR> ";" <OPT_EXPR> ")" <STATEMENT> \\

<OPT_EXPR_OR_VAR_DECL> ::= <EXPR_OR_VAR_DECL> \ | \ \epsilon \\

<OPT_EXPR> ::= <EXPR> \ | \ \epsilon \\

<BREAK_STMT> ::= "break" ";" \\

<CONTINUE_STMT> ::= "continue" ";" \\

<RETURN_STMT> ::= "return" <OPT_EXPR> ";" \\

<EXPR_STMT> ::= <EXPR_OR_VAR_DECL> ";"
\end{grammar}

\section{Expressions and Variable Declarations}
\begin{grammar}
<EXPR_OR_VAR_DECL> ::= <VAR_DECLS> \ | \ <EXPRS> \\

<VAR_DECLS> ::= <VAR_DECL> <VAR_DECLS_TAIL> \\

<VAR_DECLS_TAIL> ::= "," <VAR_DECL> <VAR_DECLS_TAIL> \ | \ \epsilon \\

<VAR_DECL> ::= <TYPE> identifier <OPT_ARRAY_SIZE> <OPT_INIT> \\

<OPT_ARRAY_SIZE> ::= "[" <E9> "]" \ | \ \epsilon \\

<OPT_INIT> ::= "=" <EXPR> \ | \ \epsilon \\

<EXPRS> ::= <EXPR> <EXPRS_TAIL> \\

<EXPRS_TAIL> ::= "," <EXPR> <EXPRS_TAIL> \ | \ \epsilon \\

<EXPR> ::= <E9> <EXPR_TAIL> \\

<EXPR_TAIL> ::= "=" <EXPR> \ | \ \epsilon \\

<E9> ::= <E8> <E9_Prime> \\
<E9_Prime> ::= "||" <E8> <E9_Prime> \ | \ \epsilon \\

<E8> ::= <E7> <E8_Prime> \\
<E8_Prime> ::= "&&" <E7> <E8_Prime> \ | \ \epsilon \\

<E7> ::= <E6> <E7_Prime> \\
<E7_Prime> ::= "|" <E6> <E7_Prime> \ | \ \epsilon \\

<E6> ::= <E5> <E6_Prime> \\
<E6_Prime> ::= "&" <E5> <E6_Prime> \ | \ \epsilon \\

<E5> ::= <E4> <E5_Prime> \\
<E5_Prime> ::= "==" <E4> <E5_Prime> \ | \ "!=" <E4> <E5_Prime> \ | \ \epsilon \\

<E4> ::= <E3> <E4_Prime> \\
<E4_Prime> ::= "<" <E3> <E4_Prime> \ | \ "<=" <E3> <E4_Prime> \ | \ ">" <E3> <E4_Prime> \ | \ ">=" <E3> <E4_Prime> \ | \ \epsilon \\

<E3> ::= <E2> <E3_Prime> \\
<E3_Prime> ::= "<<" <E2> <E3_Prime> \ | \ ">>" <E2> <E3_Prime> \ | \ \epsilon \\

<E2> ::= <E1> <E2_Prime> \\
<E2_Prime> ::= "+" <E1> <E2_Prime> \ | \ "-" <E1> <E2_Prime> \ | \ \epsilon \\

\end{grammar}
