\chapter{Grammar}

\begin{grammar}
<PROGRAM> ::= <PROGRAM\_ITEM> <PROGRAM> | $\varepsilon$

<PROGRAM\_ITEM> ::= <NON\_VOID\_TYPE> "identifier" <NON\_VOID\_DECL\_TAIL>
                  | "void" <VOID\_DECL\_TAIL>
                  | <STRUCT\_DECL>
                  | <FUNPTR\_DECL>

<NON\_VOID\_DECL\_TAIL> ::= <VARIABLE\_TAIL>
                          | <FUNCTION\_DECLARATION\_TAIL>

<VOID\_DECL\_TAIL> ::= "identifier" <FUNCTION\_DECLARATION\_TAIL>
                     | <STAR\_PLUS> "identifier" <FUNC\_OR\_VAR\_TAIL>

<FUNC\_OR\_VAR\_TAIL> ::= <VARIABLE\_TAIL>
                        | <FUNCTION\_DECLARATION\_TAIL>

<VARIABLE\_TAIL> ::= <OPT\_ARRAY\_SIZE> <OPT\_INIT> <MORE\_GLOBAL\_VARS> ";"

<FUNCTION\_DECLARATION\_TAIL> ::= "(" <OPT\_FUN\_ARGS> ")" <FUNC\_TAIL>

<FUNC\_TAIL> ::= <BLOCK\_STMT>
               | ";"

<MORE\_GLOBAL\_VARS> ::= "," "identifier" <OPT\_ARRAY\_SIZE> <OPT\_INIT> <MORE\_GLOBAL\_VARS>
                       | $\varepsilon$

<OPT\_FUN\_ARGS> ::= <FUN\_ARG> <FUN\_ARG\_TAIL>
                   | $\varepsilon$

<FUN\_ARG\_TAIL> ::= "," <FUN\_ARG> <FUN\_ARG\_TAIL>
                   | $\varepsilon$

<FUN\_ARG> ::= <TYPE> "identifier"

<STATEMENT> ::= <BLOCK\_STMT>
              | <IF\_STMT>
              | <SWITCH\_STMT>
              | <WHILE\_STMT>
              | <DO\_WHILE\_STMT>
              | <FOR\_STMT>
              | <BREAK\_STMT>
              | <CONTINUE\_STMT>
              | <RETURN\_STMT>
              | <EXPR\_STMT>

<BLOCK\_STMT> ::= "{" <STATEMENT\_STAR> "}"

<STATEMENT\_STAR> ::= <STATEMENT> <STATEMENT\_STAR>
                    | $\varepsilon$

<IF\_STMT> ::= "if" "(" <EXPR> ")" <STATEMENT> <ELSE\_PART>

<ELSE\_PART> ::= "else" <STATEMENT>
               | $\varepsilon$

<SWITCH\_STMT> ::= "switch" "(" <EXPR> ")" "{" <CASE\_DEFLT\_STMT\_STAR> "}"

<CASE\_DEFLT\_STMT\_STAR> ::= <CASE\_STMT> <CASE\_DEFLT\_STMT\_STAR>
                            | $\varepsilon$
                            | <DEFAULT\_CASE> <CASE\_STMT\_STAR>

<CASE\_STMT\_STAR> ::= <CASE\_STMT> <CASE\_STMT\_STAR>
                     | $\varepsilon$

<CASE\_STMT> ::= "case" "integer_literal" ":" <CASE\_BODY>

<CASE\_BODY> ::= <STATEMENT\_STAR>

<DEFAULT\_CASE> ::= "default" ":" <CASE\_BODY>

<WHILE\_STMT> ::= "while" "(" <EXPR> ")" <STATEMENT>

<DO\_WHILE\_STMT> ::= "do" <STATEMENT> "while" "(" <EXPR> ")" ";"

<FOR\_STMT> ::= "for" "(" <OPT\_EXPR\_OR\_VAR\_DECL> ";" <OPT\_EXPR> ";" <OPT\_EXPR> ")" <STATEMENT>

<OPT\_EXPR\_OR\_VAR\_DECL> ::= <EXPR\_OR\_VAR\_DECL>
                             | $\varepsilon$

<OPT\_EXPR> ::= <EXPR>
              | $\varepsilon$

<BREAK\_STMT> ::= "break" ";"

<CONTINUE\_STMT> ::= "continue" ";"

<RETURN\_STMT> ::= "return" <OPT\_EXPR> ";"

<EXPR\_STMT> ::= <EXPR\_OR\_VAR\_DECL> ";"

<EXPR\_OR\_VAR\_DECL> ::= <VAR\_DECLS>
                        | <EXPRS>

<VAR\_DECLS> ::= <VAR\_DECL> <VAR\_DECLS\_TAIL>

<VAR\_DECLS\_TAIL> ::= "," <VAR\_DECL> <VAR\_DECLS\_TAIL>
                     | $\varepsilon$

<VAR\_DECL> ::= <TYPE> "identifier" <OPT\_ARRAY\_SIZE> <OPT\_INIT>

<OPT\_ARRAY\_SIZE> ::= "[" <E9> "]"
                     | $\varepsilon$

<OPT\_INIT> ::= "=" <EXPR>
              | $\varepsilon$

<EXPRS> ::= <EXPR> <EXPRS\_TAIL>

<EXPRS\_TAIL> ::= "," <EXPR> <EXPRS\_TAIL>
                | $\varepsilon$

<TYPE> ::= <BASE\_TYPE> <STAR\_SEQ>
         | <TYPENAME> <STAR\_SEQ>
         | "void" <STAR\_PLUS>

<NON\_VOID\_TYPE> ::= <BASE\_TYPE> <STAR\_SEQ>
                    | <TYPENAME> <STAR\_SEQ>

<BASE\_TYPE> ::= "int"
               | "double"
               | "char"

<TYPE\_FUN\_RET> ::= <FUN\_RET\_TYPES> <STAR\_SEQ>

<FUN\_RET\_TYPES> ::= "void"
                    | <BASE\_TYPE>
                    | <TYPENAME>

<STAR\_PLUS> ::= "*" <STAR\_SEQ>

<STAR\_SEQ> ::= "*" <STAR\_SEQ>
              | $\varepsilon$

<STRUCT\_DECL> ::= "struct" "identifier" <OPT\_STRUCT\_BODY> ";"

<OPT\_STRUCT\_BODY> ::= "{" <STRUCT\_FIELDS> "}"
                      | $\varepsilon$

<STRUCT\_FIELDS> ::= <STRUCT\_FIELD> <STRUCT\_FIELDS>
                   | $\varepsilon$

<STRUCT\_FIELD> ::= <TYPE> "identifier" ";"

<FUNPTR\_DECL> ::= "typedef" <TYPE\_FUN\_RET> "(" "*" "identifier" ")" "(" <OPT\_FUNPTR\_ARGS> ")" ";"

<OPT\_FUNPTR\_ARGS> ::= <FUNPTR\_ARGS>
                      | $\varepsilon$

<FUNPTR\_ARGS> ::= <TYPE> <FUNPTR\_ARGS\_TAIL>

<FUNPTR\_ARGS\_TAIL> ::= "," <TYPE> <FUNPTR\_ARGS\_TAIL>
                       | $\varepsilon$

<EXPR> ::= <E9> <EXPR\_TAIL>

<EXPR\_TAIL> ::= "=" <EXPR>
               | $\varepsilon$

<E9> ::= <E8> <E9\_Prime>

<E9\_Prime> ::= "||" <E8> <E9\_Prime>
              | $\varepsilon$

<E8> ::= <E7> <E8\_Prime>

<E8\_Prime> ::= "&&" <E7> <E8\_Prime>
              | $\varepsilon$

<E7> ::= <E6> <E7\_Prime>

<E7\_Prime> ::= "|" <E6> <E7\_Prime>
              | $\varepsilon$

<E6> ::= <E5> <E6\_Prime>

<E6\_Prime> ::= "&" <E5> <E6\_Prime>
              | $\varepsilon$

<E5> ::= <E4> <E5\_Prime>

<E5\_Prime> ::= "==" <E4> <E5\_Prime>
              | "!=" <E4> <E5\_Prime>
              | $\varepsilon$

<E4> ::= <E3> <E4\_Prime>

<E4\_Prime> ::= "<" <E3> <E4\_Prime>
              | "<=" <E3> <E4\_Prime>
              | ">" <E3> <E4\_Prime>
              | ">=" <E3> <E4\_Prime>
              | $\varepsilon$

<E3> ::= <E2> <E3\_Prime>

<E3\_Prime> ::= "<<" <E2> <E3\_Prime>
              | ">>" <E2> <E3\_Prime>
              | $\varepsilon$

<E2> ::= <E1> <E2\_Prime>

<E2\_Prime> ::= "+" <E1> <E2\_Prime>
              | "-" <E1> <E2\_Prime>
              | $\varepsilon$

<E1> ::= <E\_UNARY\_PRE> <E1\_Prime>

<E1\_Prime> ::= "*" <E\_UNARY\_PRE> <E1\_Prime>
              | "/" <E\_UNARY\_PRE> <E1\_Prime>
              | "%" <E\_UNARY\_PRE> <E1\_Prime>
              | $\varepsilon$

<E\_UNARY\_PRE> ::= "+" <E\_UNARY\_PRE>
                  | "-" <E\_UNARY\_PRE>
                  | "!" <E\_UNARY\_PRE>
                  | "~" <E\_UNARY\_PRE>
                  | "++" <E\_UNARY\_PRE>
                  | "--" <E\_UNARY\_PRE>
                  | "*" <E\_UNARY\_PRE>
                  | "&" <E\_UNARY\_PRE>
                  | <E\_CALL\_INDEX\_MEMBER\_POST>

<E\_CALL\_INDEX\_MEMBER\_POST> ::= <F> <E\_CALL\_IDX\_MEM\_POST\_Prime>

<E\_CALL\_IDX\_MEM\_POST\_Prime> ::= <E\_CALL> <E\_CALL\_IDX\_MEM\_POST\_Prime>
                                   | <E\_INDEX> <E\_CALL\_IDX\_MEM\_POST\_Prime>
                                   | <E\_MEMBER> <E\_CALL\_IDX\_MEM\_POST\_Prime>
                                   | <E\_POST> <E\_CALL\_IDX\_MEM\_POST\_Prime>
                                   | $\varepsilon$

<E\_CALL> ::= "(" <OPT\_EXPR\_LIST> ")"

<OPT\_EXPR\_LIST> ::= <EXPR> <EXPR\_TAIL\_LIST>
                    | $\varepsilon$

<EXPR\_TAIL\_LIST> ::= "," <EXPR> <EXPR\_TAIL\_LIST>
                     | $\varepsilon$

<E\_INDEX> ::= "[" <EXPR> "]"

<E\_MEMBER> ::= "." "identifier"
              | "->" "identifier"

<E\_POST> ::= "++"
            | "--"

<F> ::= "integer_literal"
      | "double_literal"
      | "char_literal"
      | "string_literal"
      | "identifier"
      | "(" <EXPR> ")"
      | <E\_CAST>

<E\_CAST> ::= "cast" "<" <TYPE> ">" "(" <EXPR> ")"
\end{grammar}